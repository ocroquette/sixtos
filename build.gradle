plugins {
    id 'java'
    id "groovy" // For spock
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

group = 'com.github.ocroquette'
version = '1.0-SNAPSHOT'

description = """Simple HTTP server that allows to upload files with PUT and retrieve them with GET"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    // maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'io.dropwizard', name: 'dropwizard-core', version:'1.2.2'
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version:'1.2.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.11'

    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
    testCompile "org.hamcrest:hamcrest-core:1.3" // only necessary if Hamcrest matchers are used
    testRuntime "net.bytebuddy:byte-buddy:1.6.5"          // allows mocking of classes (in addition to interfaces)
    testRuntime "org.objenesis:objenesis:2.5.1" // allows mocking of classes without default constructor (together with CGLIB)
}

shadowJar {
    mergeServiceFiles()
}

jar {
    manifest {
        attributes "Main-Class": "com.github.ocroquette.sixtos.SixtosApplication"
    }
}


configurations.all {
    resolutionStrategy {
        // Workaround for version conflict because of Spock:
        // groovy.lang.GroovyRuntimeException: Conflicting module versions. Module [groovy-all is loaded in version 2.4.12 and you are trying to load version 2.4.9
        // See https://stackoverflow.com/questions/31099214/tomcat-conflicting-module-versions-module-groovy-all-is-loaded-in-version-2-3/33262366#33262366
        force 'org.codehaus.groovy:groovy-all:2.4.12'
    }
}

build.dependsOn shadowJar
